plugins {
    id 'base'
}

group = 'ai.pipestream'

// Repositories for BOM subproject
allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/ai-pipestream/*')
            credentials {
                username = findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

// Aggregate tasks to build all extensions (composite builds)
tasks.register('buildAll') {
    group = 'build'
    description = 'Build all extensions'
    dependsOn gradle.includedBuild('pipestream-quarkus-devservices').task(':buildAll')
    dependsOn gradle.includedBuild('quarkus-apicurio-registry-protobuf').task(':build')
    dependsOn gradle.includedBuild('quarkus-dynamic-grpc-extension').task(':build')
    dependsOn gradle.includedBuild('pipestream-service-registration-extension').task(':build')
}

tasks.register('testAll') {
    group = 'verification'
    description = 'Test all extensions (runs buildAll which includes tests)'
    dependsOn 'buildAll'
}

// Note: cleanAll not provided - each extension uses subproject cleans
// Use: cd <extension-dir> && ../gradlew clean

// Wrapper task for Gradle wrapper
wrapper {
    gradleVersion = '9.2.1'
    distributionType = Wrapper.DistributionType.ALL
}
