buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
    alias(libs.plugins.nmcp)
}

scmVersion {
    tag {
        prefix = 'v'
        initialVersion { config, position -> '0.7.0' }
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

allprojects {
    group = 'ai.pipestream'
    version = rootProject.scmVersion.version
}

nmcpAggregation {
    publishAllProjectsProbablyBreakingProjectIsolation()

    centralPortal {
        username = findProperty('mavenCentralUsername') ?: System.getenv('MAVEN_CENTRAL_USERNAME')
        password = findProperty('mavenCentralPassword') ?: System.getenv('MAVEN_CENTRAL_PASSWORD')
        publishingType = "AUTOMATIC"
        // Explicitly set baseUrl to use Maven Central Portal (not OSS)
        baseUrl = "https://central.sonatype.com/"
    }
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
        mavenLocal()
        // Apache Snapshots for Tika 4.0.0-SNAPSHOT
        maven {
            name = 'ApacheSnapshots'
            url = uri('https://repository.apache.org/content/repositories/snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/ai-pipestream/pipestream-platform')
            credentials {
                username = findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
    
    // Configure Java projects (excluding BOM)
    if (project.name != 'bom') {
        apply plugin: 'java'
        
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
            withSourcesJar()
            withJavadocJar()
        }
        
        // Remove JavaDoc links to internal Quarkus classes to prevent build errors
        tasks.withType(Javadoc).configureEach {
            doFirst {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }

        publishing {
            repositories {
                mavenLocal()
                // Maven Central Snapshots (Central Portal)
                maven {
                    name = 'MavenCentralSnapshots'
                    url = uri('https://central.sonatype.com/repository/maven-snapshots/')
                    credentials {
                        username = findProperty('mavenCentralUsername') ?: System.getenv('MAVEN_CENTRAL_USERNAME')
                        password = findProperty('mavenCentralPassword') ?: System.getenv('MAVEN_CENTRAL_PASSWORD')
                    }
                }
                // GitHub Packages
                maven {
                    name = 'GitHubPackages'
                    url = uri('https://maven.pkg.github.com/ai-pipestream/pipestream-platform')
                    credentials {
                        username = findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                        password = findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
                    }
                }
            }
            
            publications {
                create('maven', MavenPublication) {
                    from components.java
                    
                    pom {
                        name = project.name
                        description = project.description ?: "Pipestream Platform Component"
                        url = 'https://pipestream.ai'
                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'https://opensource.org/licenses/MIT'
                            }
                        }
                        developers {
                            developer {
                                id = 'krickert'
                                name = 'Kristian Rickert'
                            }
                        }
                        scm {
                            connection = 'scm:git:https://github.com/ai-pipestream/pipestream-platform.git'
                            developerConnection = 'scm:git:ssh://git@github.com/ai-pipestream/pipestream-platform.git'
                            url = 'https://github.com/ai-pipestream/pipestream-platform'
                        }
                    }
                }
            }
        }
    }

    signing {
        def signingKey = System.getenv("GPG_PRIVATE_KEY")
        def signingPassword = System.getenv("GPG_PASSPHRASE")
        
        // Only sign if keys are present (e.g. CI/Release)
        required { signingKey != null }
        
        if (signingKey && signingPassword) {
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications
        }
    }
}
