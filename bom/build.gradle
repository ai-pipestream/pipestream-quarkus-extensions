plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
    id 'com.gradleup.nmcp' version '1.3.0'
    id 'java-platform'
    id 'maven-publish'
    id 'version-catalog'
    id 'signing'
}

// Independent versioning for BOM using axion-release
scmVersion {
    tag {
        prefix = 'bom-v'
        initialVersion { config, position -> '0.6.0' }
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

group = 'ai.pipestream'
version = scmVersion.version
description = 'Pipestream AI BOM - Bill of Materials for the entire platform'

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform(libs.quarkus.bom)
    api platform("io.grpc:grpc-bom:${libs.versions.grpc.get()}")
    api platform("com.google.protobuf:protobuf-bom:${libs.versions.protobuf.get()}")

    constraints {
        // ========================
        // Third-party dependencies
        // ========================
        api libs.opensearch.java
        api libs.wiremock.standalone
        api libs.wiremock.grpc.extension.standalone
        api libs.apicurio.registry.java.sdk
        api libs.apicurio.registry.common
        api libs.apicurio.registry.protobuf.serde
        api libs.commonmark
        api libs.pdfbox
        api libs.password4j
        api libs.quarkus.mcp.server.sse
        api libs.smallrye.reactive.messaging.in.memory

        // Strict gRPC/Protobuf version alignment
        api("io.grpc:grpc-protobuf") { version { strictly(libs.versions.grpc.get()) } }
        api("io.grpc:grpc-stub") { version { strictly(libs.versions.grpc.get()) } }
        api("io.grpc:grpc-api") { version { strictly(libs.versions.grpc.get()) } }
        api("com.google.protobuf:protobuf-java") { version { strictly(libs.versions.protobuf.get()) } }
        api("com.google.protobuf:protobuf-java-util") { version { strictly(libs.versions.protobuf.get()) } }
        api("com.google.protobuf:protoc") { version { strictly(libs.versions.protobuf.get()) } }

        // ========================
        // Pipestream Quarkus Extensions
        // ========================
        api libs.pipestream.devservices
        api libs.pipestream.devservices.deployment

        api libs.quarkus.apicurio.registry.protobuf
        api libs.quarkus.apicurio.registry.protobuf.deployment

        api libs.quarkus.dynamic.grpc
        api libs.quarkus.dynamic.grpc.deployment

        api libs.pipestream.service.registration
        api libs.pipestream.service.registration.deployment

        // ========================
        // Pipestream Internal Libraries
        // ========================
        // grpc modules - part of platform release train
        api("ai.pipestream:grpc-stubs:${version}")
        api("ai.pipestream:grpc-google-descriptor:${version}")

        // Library modules
        api libs.pipestream.wiremock.server
        api libs.tika4.shaded

        // Sample documents test data
        api libs.pipestream.sample.doc.types
        api libs.pipestream.test.documents
        api libs.pipestream.parser.pipedoc.parsed
        api libs.pipestream.double.chunked.pipedocs
    }
}

catalog {
    versionCatalog {
        from(files("gradle/libs.versions.toml"))
    }
}

publishing {
    publications {
        create('maven', MavenPublication) {
            from components.javaPlatform
            artifactId = 'pipestream-bom'

            pom {
                name = 'Pipestream AI BOM'
                description = 'Bill of Materials for the Pipestream AI platform'
                url = 'https://github.com/ai-pipestream/pipestream-quarkus-extensions'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'krickert'
                        name = 'Kristian Rickert'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/ai-pipestream/pipestream-quarkus-extensions.git'
                    developerConnection = 'scm:git:ssh://git@github.com/ai-pipestream/pipestream-quarkus-extensions.git'
                    url = 'https://github.com/ai-pipestream/pipestream-quarkus-extensions'
                }
            }
        }

        create('catalog', MavenPublication) {
            from components.versionCatalog
            artifactId = 'pipestream-bom-catalog'

            pom {
                name = 'Pipestream AI BOM Catalog'
                description = 'Version Catalog for the Pipestream AI platform'
                url = 'https://github.com/ai-pipestream/pipestream-quarkus-extensions'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'krickert'
                        name = 'Kristian Rickert'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/ai-pipestream/pipestream-quarkus-extensions.git'
                    developerConnection = 'scm:git:ssh://git@github.com/ai-pipestream/pipestream-quarkus-extensions.git'
                    url = 'https://github.com/ai-pipestream/pipestream-quarkus-extensions'
                }
            }
        }
    }

    repositories {
        mavenLocal()

        // GitHub Packages
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/ai-pipestream/pipestream-quarkus-extensions')
            credentials {
                username = findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
            }
        }

        // Maven Central Snapshots
        maven {
            name = 'MavenCentralSnapshots'
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            credentials {
                username = findProperty('mavenCentralUsername') ?: System.getenv('MAVEN_CENTRAL_USERNAME')
                password = findProperty('mavenCentralPassword') ?: System.getenv('MAVEN_CENTRAL_PASSWORD')
            }
        }
    }
}

// nmcp plugin configuration for Maven Central publishing
nmcp {
    publishAllPublicationsToCentralPortal {
        username = findProperty('mavenCentralUsername') ?: System.getenv('MAVEN_CENTRAL_USERNAME')
        password = findProperty('mavenCentralPassword') ?: System.getenv('MAVEN_CENTRAL_PASSWORD')
        publishingType = "AUTOMATIC"
    }
}

signing {
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}
