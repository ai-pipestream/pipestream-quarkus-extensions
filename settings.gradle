pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        mavenLocal()
    }
    plugins {
        id 'io.quarkus' version "${quarkusPluginVersion}"
        id 'pl.allegro.tech.build.axion-release' version "${axionReleaseVersion}"
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

rootProject.name = 'pipestream-quarkus-extensions'

// Version catalog for the BOM and root project
dependencyResolutionManagement {
    versionCatalogs {
        libs {
            from(files("bom/gradle/libs.versions.toml"))
        }
    }
}

// BOM is a true subproject (no settings.gradle of its own)
include 'bom'

// Extensions as composite builds (each stays its own root project)
// This preserves their multi-module structure (runtime/deployment)
includeBuild 'pipestream-quarkus-devservices'
includeBuild 'quarkus-apicurio-registry-protobuf'
includeBuild 'quarkus-dynamic-grpc-extension'
includeBuild 'pipestream-service-registration-extension'
