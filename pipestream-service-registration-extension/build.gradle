import org.gradle.plugins.signing.Sign

plugins {
    id 'base'
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
    id 'com.gradleup.nmcp.aggregation' version '1.3.0'
    id 'maven-publish'
    id 'signing'
}

scmVersion {
    repository {
        directory = rootDir.parentFile.absolutePath  // Git repo is in parent (monorepo root)
    }
    tag {
        prefix = 'service-registration-v'
        initialVersion { config, position -> '0.2.0' }
    }
    monorepo {
        projectDirs = ['pipestream-service-registration-extension']
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

nmcpAggregation {
    publishAllProjectsProbablyBreakingProjectIsolation()

    centralPortal {
        username = findProperty('mavenCentralUsername') ?: System.getenv('MAVEN_CENTRAL_USERNAME')
        password = findProperty('mavenCentralPassword') ?: System.getenv('MAVEN_CENTRAL_PASSWORD')
        publishingType = "AUTOMATIC"
    }
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        // Maven Central snapshots host ai.pipestream SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        withSourcesJar()
        withJavadocJar()
    }

    group = rootProject.group
    version = scmVersion.version

    // Compute proper artifact names based on project
    ext {
        baseArtifactName = 'pipestream-service-registration-extension'
        publishedArtifactId = project.name == 'runtime' ? baseArtifactName : "${baseArtifactName}-${project.name}"
    }

    publishing {
        publications {
            create('maven', MavenPublication) {
                from components.java
                artifactId = publishedArtifactId

                pom {
                    name = publishedArtifactId
                    description = 'Quarkus Extension for Automatic Service Registration with Platform Registration Service'
                    url = 'https://github.com/ai-pipestream/pipestream-service-registration-extension'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'krickert'
                            name = 'Pipestream Engine Team'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/ai-pipestream/pipestream-service-registration-extension.git'
                        developerConnection = 'scm:git:ssh://git@github.com/ai-pipestream/pipestream-service-registration-extension.git'
                        url = 'https://github.com/ai-pipestream/pipestream-service-registration-extension'
                    }
                }
            }
        }
        repositories {
            mavenLocal()
            def ghRepo = System.getenv('GITHUB_REPOSITORY') ?: 'ai-pipestream/pipestream-service-registration-extension'
            def ghActor = System.getenv('GITHUB_ACTOR') ?: (project.findProperty('gpr.user') ?: System.getenv('GPR_USER'))
            def ghToken = System.getenv('GITHUB_TOKEN') ?: (project.findProperty('gpr.key') ?: System.getenv('GPR_TOKEN'))
            if (ghActor && ghToken) {
                maven {
                    name = 'GitHubPackages'
                    url = uri("https://maven.pkg.github.com/${ghRepo}")
                    credentials {
                        username = ghActor
                        password = ghToken
                    }
                }
            }
        }
    }

    signing {
        def signingKey = System.getenv("GPG_PRIVATE_KEY")
        def signingPassword = System.getenv("GPG_PASSPHRASE")

        if (signingKey && signingPassword) {
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications.maven
        }
    }

    // Configure Javadoc task to ignore warnings from generated protobuf code
    tasks.withType(Javadoc).configureEach { task ->
        def opts = task.options
        opts.encoding = 'UTF-8'
        opts.charSet = 'UTF-8'
        opts.locale = 'en'
        // Suppress missing Javadoc warnings (protobuf-generated classes don't have javadoc)
        opts.addStringOption('Xdoclint:-missing', '-quiet')
        // Exclude generated protobuf packages and internal packages
        task.exclude('**/generated/**', '**/build/generated/**', '**/internal/**', '**/impl/**', '**/test/**')
    }
}

// Ensure all signing tasks run before nmcp bundles and publishes artifacts
afterEvaluate {
    tasks.matching { it.name.startsWith("nmcpZipAllPublications") || it.name.startsWith("nmcpPublishAllPublicationsToCentralPortal") }
        .configureEach { nmcpTask ->
            nmcpTask.dependsOn(tasks.withType(Sign))
        }
}
