plugins {
    alias(libs.plugins.java.library)
    alias(libs.plugins.quarkus.extension)
    id 'ai.pipestream.proto-toolchain'
}

quarkusExtension {
    deploymentModule = 'pipestream-service-registration-deployment'
}

// Workaround for Gradle 9: disable validateExtension which performs unsafe configuration resolution
tasks.matching { it.name == 'validateExtension' }.configureEach {
    enabled = false
}

// ============================================================
// PROTOBUF CONFIGURATION - Using proto-toolchain plugin
// ============================================================

pipestreamProtos {
    // Fetch from Git (default) or BSR via: -PprotoSource=bsr
    sourceMode = providers.gradleProperty('protoSource').orElse('git')

    modules {
        register("registration") {
            // BSR (rate-limited, use -PprotoSource=bsr if needed)
            // bsr = "buf.build/pipestreamai/registration"

            // Git source (default - no rate limiting)
            // Allow override via env vars for CI/CD (e.g. passing tokens)
            gitRepo = System.getenv('PROTO_GIT_REPO') ?: "https://github.com/ai-pipestream/pipestream-protos.git"
            gitRef = System.getenv('PROTO_GIT_REF') ?: "main"
            gitSubdir = "registration"
        }
    }
}

// ============================================================
// END PROTOBUF CONFIGURATION
// ============================================================

configurations.configureEach {
    resolutionStrategy {
        force libs.grpc.protobuf
        force libs.grpc.stub
        force libs.grpc.netty
        force libs.grpc.core
        force libs.grpc.api
        force libs.protobuf.java
        force libs.protobuf.java.util
    }
}

dependencies {
    api platform(libs.quarkus.bom)
    api 'io.quarkus:quarkus-arc'
    api 'io.quarkus:quarkus-grpc'
    api 'io.quarkus:quarkus-smallrye-health'
    api 'io.quarkus:quarkus-vertx'

    // gRPC and Protobuf runtime - explicitly specify versions to override BOM
    api libs.grpc.protobuf
    api libs.grpc.stub
    api libs.grpc.netty
    api libs.protobuf.java
    api libs.protobuf.java.util

    // Mutiny Consul client for service discovery
    api libs.smallrye.mutiny.vertx.consul.client

    annotationProcessor enforcedPlatform(libs.quarkus.bom)
    annotationProcessor 'io.quarkus:quarkus-extension-processor'
    api "javax.annotation:javax.annotation-api:1.3.2"
}
