plugins {
    id 'base'
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
    id 'com.gradleup.nmcp.aggregation' version '1.3.0'
    id 'maven-publish'
    id 'signing'
}

scmVersion {
    repository {
        directory = rootDir.parentFile.absolutePath  // Git repo is in parent (monorepo root)
    }
    tag {
        prefix = 'apicurio-v'
        initialVersion { config, position -> '0.2.0' }
    }
    monorepo {
        projectDirs = ['quarkus-apicurio-registry-protobuf']
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

nmcpAggregation {
    publishAllProjectsProbablyBreakingProjectIsolation()

    centralPortal {
        username = findProperty('mavenCentralUsername') ?: System.getenv('MAVEN_CENTRAL_USERNAME')
        password = findProperty('mavenCentralPassword') ?: System.getenv('MAVEN_CENTRAL_PASSWORD')
        publishingType = "AUTOMATIC"
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = rootProject.group
    version = scmVersion.version

    java {
        withSourcesJar()
        withJavadocJar()
    }

    // Compute proper artifact names based on project
    ext {
        baseArtifactName = 'quarkus-apicurio-registry-protobuf'
        publishedArtifactId = project.name == 'runtime' ? baseArtifactName : "${baseArtifactName}-${project.name}"
    }

    publishing {
        publications {
            create('maven', MavenPublication) {
                from components.java
                artifactId = publishedArtifactId

                pom {
                    name = publishedArtifactId
                    description = 'Quarkus Extension for Apicurio Registry Protobuf'
                    url = 'https://github.com/ai-pipestream/pipestream-quarkus-extensions'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'krickert'
                            name = 'Kristian Rickert'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/ai-pipestream/pipestream-quarkus-extensions.git'
                        developerConnection = 'scm:git:ssh://git@github.com/ai-pipestream/pipestream-quarkus-extensions.git'
                        url = 'https://github.com/ai-pipestream/pipestream-quarkus-extensions'
                    }
                }
            }
        }
        repositories {
            mavenLocal()
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/ai-pipestream/pipestream-quarkus-extensions"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    signing {
        def signingKey = System.getenv("GPG_SIGNING_KEY")
        def signingPassword = System.getenv("GPG_SIGNING_PASSPHRASE")

        if (signingKey && signingPassword) {
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications.maven
        }
    }
}
